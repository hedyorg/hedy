@top Program { eol* (Command eol+)* Command? }
Command {
    Assign | AssignList | Ask | Clear |Print | Turtle | Sleep | Add | Remove| If | Else | ErrorInvalid
}

IfLessCommand {
    Assign | AssignList | Ask | Clear |Print | Turtle | Sleep | Add | Remove | ErrorInvalid
}

@local tokens {    
    singleQuoteStringEnd { "'" | "\n" }
    @else singleQuoteStringContent
  }
  
  @skip {} {
    singleQuotedString { "'" (singleQuoteStringContent)* singleQuoteStringEnd }
}

@local tokens {    
    doubleQuoteStringEnd { '"' | "\n" }
    @else doubleQuoteStringContent
  }
  
  @skip {} {
    doubleQuotedString { '"' (doubleQuoteStringContent)* doubleQuoteStringEnd }
}

String { doubleQuotedString | singleQuotedString }

Print { print ( String | ListAccess | Expression )+ }
Ask { Text (is+ | Op<"=">) ask+ ( String | ListAccess | Expression )+ }

AssignList { Text (is+ | Op<"=">) (Text+ | Int ) ~ambig  (Comma (Text+ | Int ))+ } // comma doesn't really parse well if it is not separated by spaces
Assign { Text (is+ | Op<"=">) (Expression ~ambig  | ListAccess)+ }
Sleep { sleep+ (Expression | ListAccess )? }
ListAccess { Text at+ (random+ | Text) }
Add { add+ Text to_list+ Text }
Remove { remove+ Text from+ Text }
Clear { clear+ }

Turtle[@isGroup=turtle] {
    Forward { forward+ (Expression | ListAccess ) } |
    Turn { turn+ (Expression | ListAccess ) } |
    Color { color+ (Expression | ListAccess)  }
}   

If { ifs+ Condition IfLessCommand? Else? }
Else { elses+ IfLessCommand? }

Condition { 
    EqualityCheck { Text is+ (String | Expression | pressed+) } |
    InListCheck { Text ins+  Text } |
    NotInListCheck { Text not_in+  Text }
}

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

Op<expr> { expr }

binaryExpression {
    Expression !times Op<"*" | "/"> Expression |
    Expression !plus Op<"+" | "-"> Expression
}

Expression {
    Int |
    Text |
    binaryExpression
}

@precedence {
    times @left,
    plus @right
}

@external specialize { Text } specializeKeyword from "./tokens" {
    ask[@name="ask"],
    at[@name="at"],
    random[@name="random"],
    ifs[@name="if"],
    elses[@name="else"]
}

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    color[@name="color"],
    sleep[@name="sleep"],
    is[@name="is"],
    add[@name="add"],
    from[@name="from"],
    remove[@name="remove"],
    to_list[@name="to_list"],
    clear[@name="clear"],    
    pressed[@name="pressed"],
    ins[@name="in"],
    not_in[@name="not_in"]
}

@tokens {
    @precedence {
        Int,
        Text
    }
    Comment { "#" ![\n]* }
    eol { "\n" }
    Comma { $[,،，、] } // every language comma is valid here
    Text { ![^\n,،，、 '"#+\-*/=]+ } // a little bit of a hack not to parse commas never, but lezer adds them as error nodes, so it still
    space { " " }
    digit { $[٠١٢٣٤٥٦٧٨٩۰۱۲۳۴۵۶۷۸۹0123456789] } // latin, arab and pa_PK (Panjabi) scripts
    Int { digit+ }
}

@skip { Comment | space }