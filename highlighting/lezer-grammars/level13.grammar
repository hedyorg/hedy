@top Program { eol* (Command eol+)* Command? }

Command {
    Assign | AssignList | Ask | Clear |Print | Play | Turtle | Sleep | Add | Remove | If | Else | Repeat | For | Define | Call | Return | ErrorInvalid
}

@local tokens {    
    singleQuoteStringEnd { "'" | "\n" }
    @else singleQuoteStringContent
  }
  
  @skip {} {
    singleQuotedString { "'" (singleQuoteStringContent)* singleQuoteStringEnd }
}

@local tokens {    
    doubleQuoteStringEnd { '"' | "\n" }
    @else doubleQuoteStringContent
  }
  
  @skip {} {
    doubleQuotedString { '"' (doubleQuoteStringContent)* doubleQuoteStringEnd }
}

String { doubleQuotedString | singleQuotedString }


Print { print+ ( ListAccess | Expression )+ }
Ask { Text (is+ | Op<"=">) ask+ ( ListAccess | Expression )+ }

AssignList { Text (is+ | Op<"=">) (Text+ | Number ) ~ambig  (Comma (Text+ | Number ))+ }
Assign { (Text | ListAccess) (is+ | Op<"=">) (Expression ~ambig  | ListAccess)+ }
Sleep { sleep+ (Expression | ListAccess )? }
ListAccess { Text at+ (random+ | Text | Number) }
Add { add+ (Expression ) toList+ Text }
Remove { remove+ (Expression ) from+ Text }
Clear { clear+ }
Play { play+ Text+ }

Turtle[@isGroup=turtle] {
    Forward { forward+ (Expression | ListAccess ) } |
    Turn { turn+ (Expression | ListAccess ) } |
    Color { color+ (Expression | ListAccess )  }
}

If { ifs+ Condition ((and+ | or+) Condition)* }
Else { elses+ } 

Condition { 
    EqualityCheck { (String | Text | Number) (is+ | Op<"=">) (Expression | pressed+) } |
    InListCheck { (String | Text | Number) ins+ Text } |
    NotInListCheck { (String | Text | Number) not_in+ Text }
}

Repeat { repeat+ (Number | Text) times+ }

For { fors+ Text ins+ (Text | range+ (Text | Number) to+ (Text | Number)) }

Define { define+ Text (with+  Arguments)? }
Call { call+ Text (with+ Arguments)? }
Return { returns+ ( ListAccess | Expression )+ }

Arguments { argument !comma (Comma argument)* }
argument { Expression | ListAccess }

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

Op<expr> { expr }

binaryExpression {
    Expression !times Op<"*" | "/"> Expression |
    Expression !plus Op<"+" | "-"> Expression
}

Expression {
    Number |
    Text |
    String |
    Call |
    binaryExpression
}

@precedence {
    times @left,
    plus @right,
    comma @left
}

@external specialize { Text } specializeKeyword from "./tokens" {
    ask[@name="ask"],
    at[@name="at"],
    random[@name="random"],
    ifs[@name="if"],
    pressed[@name="pressed"],
    elses[@name="else"],
    and[@name="and"],
    or[@name="or"],
    call[@name="call"],
    with[@name="with"]
}

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    color[@name="color"],
    sleep[@name="sleep"],
    play[@name="play"],
    is[@name="is"],
    add[@name="add"],
    from[@name="from"],
    remove[@name="remove"],
    toList[@name="toList"],
    clear[@name="clear"],    
    ins[@name="in"],
    not_in[@name="not_in"],
    repeat[@name="repeat"],
    times[@name="times"],
    fors[@name="for"],
    to[@name="to"],
    range[@name="range"],
    define[@name="define"],
    returns[@name="return"]
}
@tokens {
    @precedence {
        Number,
        Text
    }
    Comment { "#" ![\n]* }
    eol { "\n" }
    Comma { $[,،，、] } // every language comma is valid here
    Text { ![^\n,،，、 '"#+\-*/=]+ } // a little bit of a hack not to parse commas never, but lezer adds them as error nodes, so it still
    space { " " }
    digit { $[٠١٢٣٤٥٦٧٨٩۰۱۲۳۴۵۶۷۸۹0123456789] } // latin, arab and pa_PK (Panjabi) scripts
    Number { digit+ ("." digit+)? }
}

@skip { Comment | space }
