@top Program { (Command newline)* }

Command {
    Assign | AssignList | Ask | Clear |Print | Play | Turtle | Sleep | Add | Remove | ErrorInvalid
}

Print { print+ ( String | Text | ListAccess )+ }
Ask { Text is+ ask+ ( String | Text | ListAccess )+ }

// Commas in every language are allowed
AssignList { Text is+ Text+ (Op<',' | '،' | '，' | '、' > Text+)+ }
Assign { Text is+ (Text+ | ListAccess) }
Sleep { sleep+ (Text | ListAccess)? }
ListAccess { Text at+ (random+ | Text) }
Add { add+ Text+ to_list+ Text }
Remove { remove+ Text+ from+ Text }
Clear { clear+ }
Play { play+ (ListAccess | Text+) }

Turtle[@isGroup=turtle] {
    Forward { forward+ (Text | ListAccess ) } |
    Turn { turn+ (Text | ListAccess ) } |
    Color { color+ (Text | ListAccess)  }
}

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

@external specialize { Text } specializeKeyword from "./tokens" {
    ask[@name="ask"],
    at[@name="at"],
    random[@name="random"]
}

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    color[@name="color"],
    sleep[@name="sleep"],
    play[@name="play"],
    is[@name="is"],
    add[@name="add"],
    from[@name="from"],
    remove[@name="remove"],
    to_list[@name="to_list"],
    clear[@name="clear"]
}

Op<expr> { expr }

@tokens {
    Comment { "#" ![\n]* }
    newline { '\n' }
    // A char can be anything that goes after the character ! (A1)
    // Excluding the commas in different languages:
    // ، (U+00060C) ，(U+00FF0C) 、 (U+003001)
    identifierChar {
        @asciiLetter | $[_$\u{0000a1}-\u{00060b}] |
        $[$\u{00060d}-\u{003000}] | 
        $[$\u{003002}-\u{00ff0b}] | 
        $[$\u{00ff0d}-\u{10ffff}]
    }
    word { identifierChar (identifierChar | @digit)* }
    Text { word }
    space { " " }
    String {
        '"' (![\\\n"] | "\\" _)* '"' |
        "'" (![\\\n'] | "\\" _)* "'"
    }    
}

@skip { space | Comment }
