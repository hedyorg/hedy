@top Program { (Command newline)* }

Command {
    Assign | AssignList | Ask | Clear |Print | Play | Turtle | Sleep | Add | Remove | If | Else | Repeat | For | Define | Call | Return | While | ErrorInvalid
}

Print { print+ ( Expression )+ }
Ask { Text (is+ | Op<"=">) ask+ ( Expression )+ }

// Commas in every language are allowed
AssignList { Text (is+ | Op<"=">) Op<"["> ((boolean | String | Text | Number) ~ambig (Op<',' | '،' | '，' | '、' > (boolean | String | Text | Number))*)? Op<"]"> }
Assign { (Text | ListAccess) (is+ | Op<"=">) (Expression ~ambig )+ }
Sleep { sleep+ (Expression )? }
ListAccess { Text Op<"["> (random+ | Text | Number) Op<"]"> }
Add { add+ (Expression ) to_list+ Text }
Remove { remove+ (Expression ) from+ Text }
Clear { clear+ }
Play { play+ Expression }

Turtle[@isGroup=turtle] {
    Forward { forward+ (Expression ) } |
    Turn { turn+ (Expression ) } |
    Color { color+ (Expression )  }
}

If { ifs+ Condition ((and+ | or+) Condition)* }
Else { elses+ } 

Condition {
    PressedCheck { Text ~cond is+ pressed+ } |
    EqualityCheck { (Expression ) (is+ | Op<"="> Op<"=">? ) (Expression ) } |
    NotEqualCheck { (Expression ) (Op<"!=">) (Expression ) } |
    ComparisonCheck { (Expression ) (Op<">"> | Op<"<">) Op<"=">? (Expression ) } |
    InListCheck { (Expression ~cond ) ins+  Text } |
    NotInListCheck { (Expression ~cond ) not_in+  Text }
}


Repeat { repeat+ (Number | Text) times+ }

For { fors+ Text ins+ (Text | range+ (Text | Number) to+ (Text | Number)) }
While { while+ Condition ((and+ | or+) Condition)* }

Define { define+ Text (with+  Arguments)? }
Call { call+ Text (with+ Arguments)? }
Return { returns+ (Expression )+ }

Arguments { argument !comma (Op<',' | '،' | '，' | '、' > argument)* }
argument { Expression }

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

Op<expr> { expr }

binaryExpression {
    Expression !times Op<"*" | "/"> Expression |
    Expression !plus Op<"+" | "-"> Expression
}

boolean { low_true | low_false | cap_true | cap_false }

Expression {
    Number |
    boolean |
    Text |
    String |
    ListAccess |
    Call |
    binaryExpression
}

@precedence {
    times @left,
    plus @right,
    comma @left
}

@external specialize { Text } specializeKeyword from "./tokens" {
    ask[@name="ask"],
    random[@name="random"],
    ifs[@name="if"],
    pressed[@name="pressed"],
    elses[@name="else"],
    and[@name="and"],
    or[@name="or"],
    is[@name="is"],
    while[@name="while"],
    define[@name="define"],
    with[@name="with"],
    low_true[@name="true"],
    low_false[@name="false"],
    cap_true[@name="True"],
    cap_false[@name="False"]
}

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    color[@name="color"],
    sleep[@name="sleep"],
    play[@name="play"],
    add[@name="add"],
    from[@name="from"],
    remove[@name="remove"],
    to_list[@name="to_list"],
    clear[@name="clear"],    
    ins[@name="in"],
    not_in[@name="not_in"],
    repeat[@name="repeat"],
    times[@name="times"],
    fors[@name="for"],
    to[@name="to"],
    range[@name="range"],
    call[@name="call"],
    returns[@name="return"]
}


@tokens {
    @precedence {
        Number,
        Text
    }
    Comment { "#" ![\n]* }
    newline { '\n' }
    // A char can be anything that goes after the character ! (A1)
    // Excluding the commas in different languages:
    // ، (U+00060C) ，(U+00FF0C) 、 (U+003001)    identifierChar { 
    // We are also excluding the arab and Pajabi numeral systems.    
    identifierChar { 
        @asciiLetter | $[_$\u{0000a1}-\u{00060b}] |
        $[$\u{00060d}-\u{000659}] | $[$\u{000670}-\u{0006EF}] |
        $[$\u{0006FA}-\u{003000}] |
        $[$\u{003002}-\u{00ff0b}] | $[$\u{00ff0d}-\u{10ffff}]
    }
    word { identifierChar (identifierChar | @digit)* }
    Text { word }
    space { " " }
    digit { $[٠١٢٣٤٥٦٧٨٩۰۱۲۳۴۵۶۷۸۹0123456789] } // latin, arab and pa_PK (Panjabi) scripts
    Number { digit+ ("." digit+)? }
    String {
        '"' (![\\\n"] | "\\" _)* '"' |
        "'" (![\\\n'] | "\\" _)* "'"
    }    
}

@skip { Comment | space }
