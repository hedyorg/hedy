@top Program { newline* (Command newline+)* Command? }

Command {
    Assign | AssignList | Ask | Clear |Print | Play | Turtle | Sleep | Add | Remove | If | Else | Repeat | For | Define | Call | Return | ErrorInvalid
}

Print { print+ ( ListAccess | Expression )+ }
Ask { Text (is+ | Op<"=">) ask+ ( ListAccess | Expression )+ }

// Commas in every language are allowed
AssignList { Text (is+ | Op<"=">) (Text+ | Number ) ~ambig  (Op<',' | '،' | '，' | '、' > (Text+ | Number ))+ }
Assign { (Text | ListAccess) (is+ | Op<"=">) (Expression ~ambig  | ListAccess)+ }
Sleep { sleep+ (Expression | ListAccess )? }
ListAccess { Text at+ (random+ | Text | Number) }
Add { add+ (Expression ) to_list+ Text }
Remove { remove+ (Expression ) from+ Text }
Clear { clear+ }
Play { play+ (ListAccess | Expression) }

Turtle[@isGroup=turtle] {
    Forward { forward+ (Expression | ListAccess ) } |
    Turn { turn+ (Expression | ListAccess ) } |
    Color { color+ (Expression | ListAccess )  }
}

If { ifs+ Condition ((and+ | or+) Condition)* }
Else { elses+ } 

Condition {    
    PressedCheck { Text ~cond is+ pressed+ } |
    EqualityCheck { (Expression ) (is+ | Op<"="> Op<"=">? ) (Expression ) } |
    NotEqualCheck { (Expression ) (Op<"!=">) (Expression ) } |
    ComparisonCheck { (Expression ) (Op<">"> | Op<"<">) Op<"=">? (Expression ) } |
    InListCheck { (Expression ~cond ) ins+  Text } |
    NotInListCheck { (Expression ~cond ) not_in+  Text }
}

Repeat { repeat+ (Number | Text) times+ }

For { fors+ Text ins+ (Text | range+ (Text | Number) to+ (Text | Number)) }

Define { define+ Text (with+  Arguments)? }
Call { call+ Text (with+ Arguments)? }
Return { returns+ ( ListAccess | Expression )+ }

Arguments { argument !comma (Op<',' | '،' | '，' | '、' > argument)* }
argument { Expression | ListAccess }

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

Op<expr> { expr }

binaryExpression {
    Expression !times Op<"*" | "/"> Expression |
    Expression !plus Op<"+" | "-"> Expression
}

Expression {
    Number |
    Text |
    String |
    Call |
    binaryExpression
}

@precedence {
    times @left,
    plus @right,
    comma @left
}

@external specialize { Text } specializeKeyword from "./tokens" {
    ask[@name="ask"],
    at[@name="at"],
    random[@name="random"],
    ifs[@name="if"],
    pressed[@name="pressed"],
    elses[@name="else"],
    and[@name="and"],
    or[@name="or"],
    is[@name="is"],
    define[@name="define"],
    with[@name="with"]
}

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    color[@name="color"],
    sleep[@name="sleep"],
    play[@name="play"],
    add[@name="add"],
    from[@name="from"],
    remove[@name="remove"],
    to_list[@name="to_list"],
    clear[@name="clear"],    
    ins[@name="in"],
    not_in[@name="not_in"],
    repeat[@name="repeat"],
    times[@name="times"],
    fors[@name="for"],
    to[@name="to"],
    range[@name="range"],
    call[@name="call"],   
    returns[@name="return"]    
}

@tokens {
    @precedence {
        Number,
        Text
    }
    symbol { $[%&().:;?@\[\]\\\^\{\}`~$_] }
    Comment { "#" ![\n]* }
    newline { '\n' }
    // A char can be anything that goes after the character ! (A1)
    // Excluding the commas in different languages:
    // ، (U+00060C) ，(U+00FF0C) 、 (U+003001)
    // We are also excluding the arab and Pajabi numeral systems.
    identifierChar { 
        @asciiLetter | 
        $[\u{0000a1}-\u{00060b}] | $[\u{00060d}-\u{000659}] |
        $[\u{000670}-\u{0006EF}] | $[\u{0006FA}-\u{003000}] |
        $[\u{003002}-\u{00ff0b}] | $[\u{00ff0d}-\u{10ffff}]
    }
    Text { (identifierChar | symbol | digit)+ }
    space { " " }
    digit { $[٠١٢٣٤٥٦٧٨٩۰۱۲۳۴۵۶۷۸۹0123456789] } // latin, arab and pa_PK (Panjabi) scripts
    Number { digit+ ("." digit+)? }
    String {
        '"' ( ![\\\n"] )* '"' |
        "'" ( ![\\\n'] )* "'"
    } 
}

@skip { Comment | space }
