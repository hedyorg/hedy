%import common.NUMBER -> NUMBER
%import common.SIGNED_INT -> INT
_SPACE: " "+

_COMMA: _SPACE? ("," | "،") _SPACE?

start: program

program: _EOL* (command _EOL+)* command?

command: print
  | ask
  | echo
  | turtle
  | error_invalid_space
  | comment
  | error_invalid

print: "print" (_SPACE text)?

ask: "ask" (_SPACE text)?

echo: "echo" (_SPACE text)?

turtle: "forward" (_SPACE (text | NUMBER))? -> forward
  | "turn" (_SPACE text)? -> turn

error_invalid_space: _SPACE text?

error_invalid: textwithoutspaces text?

comment: "#" /([^\n]+)/

_EOL: "\r"? "\n"

text: /([^\n]+)/ -> text

textwithoutspaces: /([^\n *+-\/]+)/ -> text

NAME: ID_START ID_CONTINUE*

ID_START: /[\p{Lu}\p{Ll}\p{Lt}\p{Lm}\p{Lo}\p{Nl}_]+/

ID_CONTINUE: ID_START
  | /[\p{Mn}\p{Mc}\p{Nd}\p{Pc}·]+/

_END_BLOCK: "end-block"
