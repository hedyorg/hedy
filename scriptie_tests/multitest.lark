%import common.DIGIT
%import common.NEWLINE
%import common.SIGNED_INT -> NUMBER
%import common.WS_INLINE
ID_CONTINUE: ID_START | /[\p{Mn}\p{Mc}\p{Nd}\p{Pc}Â·]+/
ID_START: /[\p{Lu}\p{Ll}\p{Lt}\p{Lm}\p{Lo}\p{Nl}_]+/
NAME: ID_START ID_CONTINUE*
PUNCTUATION: _EXCLAMATION_MARK | _QUESTION_MARK | _PERIOD //uppercase places tokens in tree
_ADD: "+"
_AND: "and"
_ASK : "ask"
_AT: "at"
_COLON: ":"
_COMMA: ","
_DIVIDE: "/"
_DOUBLE_EQUALS: "=="
_ECHO : "echo"
_ELIF: "elif"
_ELSE: "else"
_END_BLOCK: "end-block"
_EOL: "\r"?"\n"
_EQUALS: "="
_EXCLAMATION_MARK: "!"
_FOR: "for"
_FORWARD : "forward"
_HASH: "#"
_IF: "if"
_IN: "in"
_INPUT: "input"
_IS: "is"
_LARGER: ">"
_LARGER_EQUALS: ">="
_LEFT_BRACKET : "("
_LEFT_SQUARE_BRACKET : "["
_LENGTH: "length"
_MULTIPLY: "*"
_NOT_EQUALS: "!="
_OR: "or"
_PERIOD: "."
_PRINT : "print"
_QUESTION_MARK: "?"
_RANGE: "range"
_REPEAT: "repeat"
_RIGHT_BRACKET : ")"
_RIGHT_SQUARE_BRACKET : "]"
_SMALLER : "<"
_SMALLER_EQUALS : "<="
_SPACE: " "+
_STEP: "step"
_SUBTRACT: "-"
_TIMES: "times"
_TO: "to"
_TURN: "turn"
_WHILE: "while"
ask: var _SPACE _IS _SPACE _ASK (_SPACE (_SPACE| list_access | quoted_text | var_access)*)?
ask_no_quotes: var _SPACE _IS _SPACE _ASK _SPACE (_SPACE| list_access | textwithoutspaces | punctuation)*  -> print_nq
assign: var _SPACE _IS _SPACE text -> assign
assign_list: var _SPACE _IS _SPACE text_list ((_COMMA _SPACE|_COMMA) text_list)+
command: print | ask | turtle | assign_list | ask_no_quotes| assign | print_no_quotes | invalid //catch all at the end
echo: _ECHO (_SPACE text)?
index : NUMBER //todo FH-June 21 : why does this need its own rule? can't it just be a token
invalid: textwithoutspaces text?
invalid_space: _SPACE text
list_access: var _SPACE _AT _SPACE (index | random) -> list_access //todo: could be merged with list_access_var?
print: _PRINT (_SPACE (_SPACE| list_access | quoted_text | var_access)*)?  -> print
print_no_quotes: _PRINT _SPACE text -> print_nq
program: _EOL* (command (_SPACE)* _EOL+)* (command (_SPACE)*)? //lines may end on spaces and might be separated by many newlines
punctuation : PUNCTUATION -> punctuation
quoted_text: /'((?:[^\\']|\\.)*)'/ -> text //text can be between single quotes, but quotes may be escaped with \
quoted_text_no_escape: /'([^']*)'/ -> text //simply all between quotes should this be used at earlier levels?
random : "random" //random needs to appear in the tree for further processing so does not start with _ or is uppercase
start: program
text: /([^\n,]+)/ -> text
text_ask: /([^\r\n!?.]+)/ -> text //as ask may contain commas (punctionation is still needed separately so they can be printed after vars)
text_list: /([^\r\n,]+)/ -> text // list elements may contain punctionationm but not commas or course, these are separators
textwithoutspaces: /([^\r\n, *+-\/]+)/ -> text
turtle: _FORWARD (_SPACE (NUMBER | list_access | textwithoutspaces))? -> forward 
      | _TURN (_SPACE (list_access | textwithoutspaces))? -> turn
var: NAME -> var
var_access : NAME
