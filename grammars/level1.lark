//symbols. they start with an underscore so they don't appear in the parse tree (Lark convention)

_SPACE: " "+
_COMMA: ","
_COLON: ":"
_LEFT_BRACKET : "("
_RIGHT_BRACKET : ")"
_LEFT_SQUARE_BRACKET : "["
_RIGHT_SQUARE_BRACKET : "]"
_HASH: "#"
_SMALLER : "<"
_LARGER: ">"
_EQUALS: "="
_DOUBLE_EQUALS: "=="
_NOT_EQUALS: "!="
_SMALLER_EQUALS : "<="
_LARGER_EQUALS: ">="
_EXCLAMATION_MARK: "!"
_QUESTION_MARK: "?"
_PERIOD: "."

_PRINT : "print"
_ASK : "ask"
_ECHO : "echo"
_FORWARD : "forward"
_TURN: "turn"

//level 2
_IS: "is"
_AT: "at"
random : "random" //random needs to appear in the tree for further processing so does not start with _ or is uppercase

//level 4
_IN: "in"
_IF: "if"
_ELSE: "else"
_AND: "and"

//level 5
_REPEAT: "repeat"
_TIMES: "times"

//level 6
_MULTIPLY: "*"
_ADD: "+"
_SUBTRACT: "-"
_DIVIDE: "/"

//level 8
_FOR: "for"
_RANGE: "range"
_TO: "to"
_STEP: "step"

//level 9
_ELIF: "elif"

//level 11
_INPUT: "input"

//level 14
_OR: "or"

//level 17
_WHILE: "while"

//level 19
_LENGTH: "length"

start: program
program: _EOL* (command _EOL+)* command?
command: print | ask | echo | turtle | invalid_space | invalid

print: _PRINT (_SPACE text)?
ask: _ASK (_SPACE text)?
echo: _ECHO (_SPACE text)?
turtle: _FORWARD (_SPACE NUMBER)? -> forward | _TURN (_SPACE text)? -> turn
invalid_space: _SPACE text
invalid: textwithoutspaces text?

_EOL: "\r"?"\n"

text: /([^\n]+)/ -> text //anything can be parsed except for a newline
textwithoutspaces: /([^\n *+-\/]+)/ -> text //anything can be parsed except for spaces (plus: a newline)

%import common.DIGIT
%import common.WS_INLINE
%import common.NEWLINE
%import common.SIGNED_INT -> NUMBER

// FH Sept 2021: More info on this variable format: https://www.unicode.org/reports/tr31/tr31-1.html
// Exact grammar stolen from: https://lark-parser.readthedocs.io/en/latest/classes.html

NAME: ID_START ID_CONTINUE*
ID_START: /[\p{Lu}\p{Ll}\p{Lt}\p{Lm}\p{Lo}\p{Nl}_]+/
ID_CONTINUE: ID_START | /[\p{Mn}\p{Mc}\p{Nd}\p{Pc}Â·]+/

// Internal symbol added by the preprocess_blocks function to indicate the end of blocks
// FIXME ensure that the user can't write this token
_END_BLOCK: "end-block"
